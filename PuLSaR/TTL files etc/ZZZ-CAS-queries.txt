prefix sp: <http://www.broker-cloud.eu/service-descriptions/CAS/service-provider#> 
prefix cas: <http://www.broker-cloud.eu/service-descriptions/CAS/broker#> 
prefix owl: <http://www.w3.org/2002/07/owl#>  
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>  
prefix xml: <http://www.w3.org/XML/1998/namespace#> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#> 
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
prefix foaf: <http://xmlns.com/foaf/0.1/> 
prefix dcterms: <http://purl.org/dc/terms/> 
prefix usdl-sla: <http://www.linked-usdl.org/ns/usdl-sla#> 
prefix usdl-sla-cb: <http://www.linked-usdl.org/ns/usdl-core/cloud-broker#> 
prefix usdl-core: <http://www.linked-usdl.org/ns/usdl-core#> 
prefix usdl-core-cb: <http://www.linked-usdl.org/ns/usdl-core/cloud-broker#> 
prefix usdl-business-roles: <http://www.linked-usdl.org/ns/usdl-business-roles#> 
prefix fc: <http://www.broker-cloud.eu/service-descriptions/CAS/categories#>
prefix gr: <http://purl.org/goodrelations/v1#> 
prefix skos: <http://www.w3.org/2004/02/skos/core#>


#
#  QUERY FOR ALL RELEVANT (wrt classification dimensions) APP SERVICE MODEL INSTANCES
#  EACH (SERVICE, SERVICE MODEL INSTANCE, SERVICE-LEVEL-PROFILE) TRIPLET DEFINE A DISTINCT, SELLABLE "OFFER" FROM CONSUMER'S PERSPECTIVE
#
select distinct ?bpi ?bpsmClass ?srv ?sm ?slp ?title ?creatorName ?creatorLogo ?creatorHomepage
where { 
	?srv a ?srvClass .
	?srvClass rdfs:subClassOf * usdl-core:Service .
	?srv usdl-core-cb:hasServiceModel ?sm. 
	?sm gr:isVariantOf ?bpi .
	#
	?srv dcterms:title ?title .
	?srv dcterms:creator ?creator .
	?creator gr:legalName ?creatorName .
	optional { ?creator foaf:homepage ?creatorHomepage } .
	optional { ?creator foaf:logo ?creatorLogo } .
	#
	?sm a ?bpsmClass .
	?bpsmClass rdfs:subClassOf * usdl-core:ServiceModel .
	#
	?sm ?hasSLP ?slp.
	?hasSLP rdfs:subPropertyOf * usdl-sla:hasServiceLevelProfile .
	#
	# Classification Filter
	?sm usdl-core-cb:hasClassificationDimension fc:projectmanagement, fc:automobile, fc:calendar.
}
order by ?srv ?sm ?slp


#
#  RETRIEVE APP SERVICE MODEL SERVICE-LEVEL-PROFILE DEFAULT VALUES
#
select ?allowedValues ?defVal ?defValLabel ?uom ?minVal ?maxVal
where {
	# BROKER POLICY
	bind ( cas:CASServiceLevelProfileGold as ?slp ).
	?bpi ?hasSLP ?slp .
	?hasSLP rdfs:subPropertyOf * usdl-sla:hasServiceLevelProfile.
	?bpsmClass rdfs:subClassOf usdl-core:ServiceModel .
	?bpi a ?bpsmClass .
	# SLP
	?slp a ?slpClass .
	?slpClass rdfs:subClassOf * usdl-sla:ServiceLevelProfile .
	?slp ?hasSL ?sl .
	?hasSL rdfs:subPropertyOf * usdl-sla:hasServiceLevel .
	# SL
	?sl a ?slClass .
	?slClass rdfs:subClassOf * usdl-sla:ServiceLevel .
	?sl ?hasSLE ?sle .
	?hasSLE rdfs:subPropertyOf * usdl-sla:hasServiceLevelExpression .
	# SLE
	?sle a ?sleClass .
	?sleClass rdfs:subClassOf * usdl-sla:ServiceLevelExpression .
	?sle ?hasVar ?var .
	?hasVar rdfs:subPropertyOf * usdl-sla:hasVariable .
	# VAR
	?var a ?varClass .
	?varClass rdfs:subClassOf * usdl-sla:Variable .
	?var ?hasDef ?defVal .
	{ ?hasDef rdfs:subPropertyOf * usdl-sla-cb:hasDefaultQuantitativeValue }
	union
	{ ?hasDef rdfs:subPropertyOf * usdl-sla-cb:hasDefaultQualitativeValue } .
	?hasDef rdfs:range ?allowedValues .
	# DEFAULT VALUES
	optional { ?defVal rdfs:label ?defValLabel } .
	optional { ?defVal gr:hasUnitOfMeasurement ?uom } .
	optional { ?defVal gr:hasMinValue ?minVal } .
	optional { ?defVal gr:hasMaxValue ?maxVal } .
}
order by ?allowedValues 


#
#  RETRIEVE APP SERVICE MODEL INSTANCE VALUES
#
select ?allowedValues ?val ?valLabel ?uom ?minVal ?maxVal
where {
	bind ( sp:ServiceModelAddressApp as ?sm ) .
	?sm ?hasVal ?val .
	?hasVal rdfs:range ?allowedValues .
	{ ?hasVal rdfs:subPropertyOf * gr:quantitativeProductOrServiceProperty }
	union
	{ ?hasVal rdfs:subPropertyOf * gr:qualitativeProductOrServiceProperty }
	union
	{ ?hasVal rdfs:subPropertyOf * gr:datatypeProductOrServiceProperty } .
	optional { ?val rdfs:label ?valLabel } .
	optional { ?val gr:hasUnitOfMeasurement ?uom } .
	optional { ?val gr:hasMinValue ?minVal } .
	optional { ?val gr:hasMaxValue ?maxVal } .
}
order by ?allowedValues 

